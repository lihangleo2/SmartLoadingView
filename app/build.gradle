apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lihang.mysmartloadingview"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 3
        versionName "2.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }

    //签名 (需要签名文件)
    signingConfigs {
        key {
            storeFile file("atman.jks")//签名文件
            storePassword "5888062"
            keyAlias "fastMedical"
            keyPassword "5888062"//签名密码
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.key
        }
    }

    //多渠道打包
    flavorDimensions "mark" //相当于一个标记
    productFlavors {
        medical { dimension "mark" }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }


    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [ATMAN_CHANNEL_VALUE: name]
    }

    //自定义打包时apk名字
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            // abc_渠道名_版本名.apk  还可以拼接其他app内容：variant.versionCode  variant.buildType.name
            outputFileName = "ShadowLayout_${variant.versionName}_${variant.buildType.name}_${new Date().format("yyyy-MM-dd_HH_mm")}.apk"

        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'
    implementation project(':smartloadview')
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
}
